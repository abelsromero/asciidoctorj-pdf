dependencies {
  testImplementation ("org.asciidoctor:asciidoctorj:$asciidoctorJVersion") {
    exclude group:'org.jruby'
  }

  testImplementation "org.jruby:jruby-complete:$jrubyVersion"


  gems("rubygems:asciidoctor-pdf:$asciidoctorPdfGemVersion") {
    // Exclude gems provided by AsciidoctorJ core
    exclude module: 'asciidoctor'
    exclude module: 'thread_safe'
    exclude module: 'prawn'
    exclude module: 'addressable'
    exclude module: 'rexml'
    exclude module: 'public_suffix'
    exclude module: 'ttfunk'
    exclude module: 'concurrent-ruby'
  }
  gems "rubygems:concurrent-ruby:$concurrentRubyVersion"
  gems "rubygems:thread_safe:$threadSafeGemVersion"

  gems "rubygems:prawn:$prawnGemVersion"
  gems "rubygems:prawn-svg:$prawnSvgGemVersion"
  gems "rubygems:rghost:$rghostGemVersion"
  gems "rubygems:addressable:$addressableVersion"
  gems "rubygems:public_suffix:$public_suffixVersion"
  gems "rubygems:text-hyphen:$textHyphenVersion"
  gems "rubygems:ttfunk:$ttfunkGemVersion"
  gems "rubygems:css_parser:$cssParserGemVersion"

  testImplementation "org.apache.pdfbox:pdfbox:$pdfboxVersion"
}

def gemFiles = fileTree("${project.buildDir}/.gems") {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/*/data/fonts/**"
  include "gems/*/data/themes/**"
  include "gems/asciidoctor-pdf-$asciidoctorPdfGemVersion/data/**"
  // Accommodate Prawn's non-conforming packaging
  include "gems/prawn-*/VERSION"
  include "gems/prawn-*/data/*.txt"
  include "gems/prawn-*/data/encodings/*"
  // Include icon fonts
  include "gems/prawn-icon-*/data/fonts/**"
  // Include required data file from the addressable gem
  include "gems/addressable-*/data/*.data"
}

jrubyPrepare {
  doLast {
    copy { // bundles the gems inside this artifact
      from gemFiles
      into preparedGems
    }
  }
}

ext.publicationName = "mavenAsciidoctorJPdf"

apply from: rootProject.file('gradle/publish.gradle')
apply from: rootProject.file('gradle/signing.gradle')
