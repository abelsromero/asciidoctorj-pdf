import org.ysb33r.gradle.jruby.api.base.tasks.JRubyPrepare

/*
    adding the plugin jars to the classpath to apply them later.
    currently the new plugins DSL does apply them directly.
    there are other limitations too. See https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
    we don't need to apply the jruby plugin on the rootProject.
*/
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  }
  dependencies {
    classpath 'org.ysb33r.jruby.base:org.ysb33r.jruby.base.gradle.plugin:2.0.0'
    classpath 'org.ysb33r.jruby.resolver:org.ysb33r.jruby.resolver.gradle.plugin:2.0.0'
  }
}

// modern plugins config
plugins {
  id "signing"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

// TIP use -PpublishRelease=true to active release behavior regardless of the version
status = project.hasProperty('publishRelease') && project.publishRelease.toBoolean() ?
    'release' : ((version == 'unspecified' || version.endsWith('-SNAPSHOT')) ? 'snapshot' : 'release')

ext {
  buildDateTime = new Date()
  (buildDateOnly, buildTimeOnly) = new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss.SSSZ').format(buildDateTime).split(' ')
  statusIsRelease = (status == 'release')

  // jar versions
  jrubyVersion = '10.0.0.1'
  pdfboxVersion = '3.0.0'
  junitVersion = '5.10.0'
  assertjVersion = '3.24.2'

  // gem versions
  // TODO bump to 3
  asciidoctorJVersion = project.hasProperty('asciidoctorJVersion') ? project.asciidoctorJVersion : '2.5.13'
  asciidoctorPdfGemVersion = project.hasProperty('asciidoctorPdfGemVersion') ? project.asciidoctorPdfGemVersion : '2.3.19'

  addressableVersion = '2.8.7'
  concurrentRubyVersion = '1.1.10'
  public_suffixVersion = '1.4.6'
  prawnGemVersion = project.hasProperty('prawnGemVersion') ? project.prawnGemVersion : '2.4.0'
  prawnSvgGemVersion = "0.34.2"
  rghostGemVersion = '0.9.9'
  threadSafeGemVersion = '0.3.6'
  ttfunkGemVersion = '1.7.0'
  cssParserGemVersion = '1.21.1'
  textHyphenVersion='1.4.1'
}

allprojects {
  group = 'org.asciidoctor'
  defaultTasks 'check'
}

subprojects {
  // NOTE applying Java plugin changes the status; take steps to preserve value
  def _status = status
  apply plugin: 'java'
  apply from: "$rootDir/gradle/providedConfiguration.gradle"

  status = _status

  // NOTE sourceCompatibility & targetCompatibility are set in gradle.properties to meet requirements of Gradle
  // Must redefine here to work around a bug in the Eclipse plugin
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

  plugins.withType(JavaPlugin).configureEach {
    project.tasks.withType(JavaCompile).configureEach { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 11
      }
      if (project.hasProperty("showDeprecation")) {
        options.compilerArgs << "-Xlint:deprecation"
      }
      if (project.hasProperty("showUnchecked")) {
        options.compilerArgs << "-Xlint:unchecked"
      }
    }
    project.tasks.withType(GroovyCompile).configureEach { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 8
      }
    }
  }

  repositories {
    if (project.hasProperty('useMavenLocal') && project.useMavenLocal.toBoolean()) {
      mavenLocal()
    }

    mavenCentral()
  }

  dependencies {
    testImplementation(platform("org.junit:junit-bom:$junitVersion"))
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
  }

  test {
    useJUnitPlatform()
    forkEvery = 10
    minHeapSize = '128m'
    maxHeapSize = '1024m'
    if (JavaVersion.current().isJava8Compatible()) {
      jvmArgs '-XX:-UseGCOverheadLimit'
    } else {
      jvmArgs '-XX:MaxPermSize=256m', '-XX:-UseGCOverheadLimit'
    }

    testLogging {
      exceptionFormat = 'full'
      // events 'passed', 'failed', 'skipped', 'standard_out', 'standard_error'
      // events 'standard_out', 'standard_error'
      afterSuite { desc, result ->
        if (!desc.parent && logger.infoEnabled) {
          logger.info "Test results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        }
      }
    }
  }

}

configure(subprojects.findAll {it.name != 'itest'}) {
  apply from: rootProject.file('gradle/versioncheck.gradle')

  apply plugin: 'org.ysb33r.jruby.resolver'
  apply plugin: 'org.ysb33r.jruby.base'

  repositories {
    ruby.gems()
  }

  configurations {
    runtimeGems {
      canBeResolved(true)
    }
  }

  if (JavaVersion.current().isJava8Compatible()) {
    javadoc {
      // Oracle JDK8 likes to fail the build over spoiled HTML
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

}

configure(subprojects.findAll { it.name != 'itest'}) {

  jruby {
    jrubyVersion = jrubyVersion
  }

  compileJava {
    options.release = 11
  }

  // QUESTION is this the right place to insert this task dependency in the lifecycle?
  // IMPORTANT The TMP or TEMP environment variable must be set for the gem install command to work on Windows
//  processResources.dependsOn jrubyPrepare
}

nexusPublishing {
  repositories {
    sonatype {
      if (project.hasProperty("sonatypeUsername")) {
        username = project.sonatypeUsername
      }
      if (project.hasProperty("sonatypePassword")) {
        password = project.sonatypePassword
      }
      repositoryDescription = "Release ${project.group} ${project.version}"
    }
  }
  clientTimeout = Duration.ofMinutes(5)
}
